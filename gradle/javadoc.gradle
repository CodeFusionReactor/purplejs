apply plugin: 'java'

task allJavadoc( type: Javadoc ) {
    group = 'Documentation'
    description = 'Generates java documentation for all projects.'

    source subprojects.collect { it.sourceSets.main.allJava }
    classpath = files( subprojects.collect { it.sourceSets.main.compileClasspath } )
    destinationDir = file( "${buildDir}/docs/javadoc" )

    excludes = ['**/internal/**']
    failOnError = false

    options {
        links( "http://docs.oracle.com/javase/8/docs/api/" )
        links( "https://google.github.io/guava/releases/${versions.guava}/api/docs/" )
    }
}

task javadocJar( type: Jar ) {
    classifier 'javadoc'
    from allJavadoc
}

artifacts {
    archives javadocJar
}

jar.enabled = false
