apply plugin: 'java'
apply plugin: 'jacoco'
apply from: "$rootDir/gradle/dependencies.gradle"

def defaultEncoding = 'UTF-8'

sourceCompatibility = '1.8'
targetCompatibility = sourceCompatibility

tasks.withType( AbstractCompile ) {
    options.encoding = defaultEncoding
    options.compilerArgs = ['-Xlint:-options']
}

dependencies {
    testCompile "junit:junit:${versions.junit}"
    testCompile "org.mockito:mockito-core:${versions.mockito}"
}

task sourceJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allJava
}

artifacts {
    archives sourceJar
}

test {
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m'

    testLogging {
        exceptionFormat "full"
    }
}

jar {
    manifest {
        attributes( ['Implementation-Version': project.version] )
    }
}
