apply plugin: 'java'
apply plugin: 'groovy'
apply plugin: 'jacoco'
apply plugin: 'maven-publish'
apply from: "$rootDir/gradle/pom.gradle"

configurations {
    testOutput.extendsFrom testRuntime
}

dependencies {
    testOutput sourceSets.test.output
}

sourceCompatibility = '1.8'
targetCompatibility = sourceCompatibility

tasks.withType( AbstractCompile ) {
    options.encoding = 'UTF-8'
    options.compilerArgs = ['-Xlint:-options']
}

javadoc {
    excludes = ['**/internal/**']

    options {
        links( "http://docs.oracle.com/javase/8/docs/api/" )
        links( "https://google.github.io/guava/releases/${versions.guava}/api/docs/" )
    }
}

task sourceJar( type: Jar ) {
    classifier 'sources'
    from sourceSets.main.allJava
}

task javadocJar( type: Jar ) {
    classifier 'javadoc'
    from javadoc
}

artifacts {
    archives sourceJar
    archives javadocJar
}

test {
    systemProperty 'java.awt.headless', 'true'
    jvmArgs '-Xmx2048m'

    testLogging {
        exceptionFormat "full"
    }
}

jar {
    manifest {
        attributes( ['Implementation-Version': project.version] )
    }
}

jacocoTestReport {
    group = 'Coverage'
    description = 'Generates a single report from single project.'
}

publishing {
    publications {
        mavenJava( MavenPublication ) {
            from components.java
            artifact sourceJar
            artifact javadocJar
        }
    }
}
